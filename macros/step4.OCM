new Y
@$ ================================================================
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ 
@$ step4.OCM 
@$ Enter data interactively and
@$ calculate G curves in the ordered FCC in the Fe-Ni system
@$ ================================================================
@$
@&

set echo

@$ Enter the elements and their reference states
enter element Fe Iron BCC 55.847 0 0 

enter element Ni Nickel FCC 58.69 0 0

@$ These functions describe the end-member energies at Fe3Ni, Fe2Ni2 and FeNi3
@$ respectivly.  The VASP energies relative to pure Fe amd Ni as fcc are:
@$ Fe3Ni1 -0.071689 eV for 1 atom??
@$ Fe2Ni2 -0.138536 eV for 1 atom??
@$ Fe1Ni3 -0.125748 eV for 1 atom??
@$ To modify to J/mol atoms multiply with 96500
@$ bond energy multiplied with 3, 4 and 3 respectively.

enter tp-sym evtoj constant 96500

enter tp-sym GA3B1 fun 1 -0.071689*evtoj;,,,,,
enter tp-sym GA2B2 fun 1 -0.138536*evtoj;,,,,,
enter tp-sym GA1B3 fun 1 -0.125748*evtoj;,,,,,
 
@$ We set a positive regular solution parameter
enter tp-sym L0 fun 1 12000; ,,,,,

@$ this is an approximate SRO contribution to the LRO phase.  It is
@$ set to about a quater of the L1_0 ordering energy, 
@$ equal to the Fe-Ni bond energy
enter tp-sym GSRO fun 1 -0.034*evtoj;,,,,,

@$ Using the partitioned model the contribition from the ordered parameters
@$ will cancel when the phase is disordered.  If we want them to contribute
@$ we must add them to the disordered part
enter tp-sym LD0 fun 1 GA3B1+1.5*GA2B2+GA1B3+1.5*GSRO;,,,,,,
enter tp-sym LD1 fun 1 2*GA3B1-2*GA1B3;,,,,,,
enter tp-sym LD2 fun 1 GA3B1-1.5*GA2B2+GA1B3-1.5*GSRO;,,,,,,

@$ ==================================================
@$ This is an fcc phase with lro but no explicit sro
@$ described with the sublattice model
enter phase PARTITIONED_FCC 
CEF
4 .25 Fe NI; .25 Fe NI; .25 Fe NI; .25 Fe NI;

@&
@$ we must add disordered set before entering parameters
amend phase part dis 4 yes

@$ We do not use the F option which would reduce the number of parameters

enter param G(part,Fe:Fe:Fe:Ni),,GA3B1; 6000 N test
enter param G(part,Fe:Fe:Ni:Fe),,GA3B1; 6000 N test
enter param G(part,Fe:Ni:Fe:Fe),,GA3B1; 6000 N test
enter param G(part,Ni:Fe:Fe:Fe),,GA3B1; 6000 N test
enter param G(part,Fe:Ni:Ni:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Fe:Ni:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Ni:Fe:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Ni:Ni:Fe),,GA1B3; 6000 N test
enter param G(part,Fe:Fe:Ni:Ni),,GA2B2; 6000 N test
enter param G(part,Fe:Ni:Fe:Ni),,GA2B2; 6000 N test
enter param G(part,Ni:Fe:Fe:Ni),,GA2B2; 6000 N test
enter param G(part,Fe:Ni:Ni:Fe),,GA2B2; 6000 N test
enter param G(part,Ni:Fe:Ni:Fe),,GA2B2; 6000 N test
enter param G(part,Ni:Ni:Fe:Fe),,GA2B2; 6000 N test

enter param G(part,Fe,Ni:Fe,Ni:*:*),,GSRO; 6000 N test
enter param G(part,Fe,Ni:*:Fe,Ni:*),,GSRO; 6000 N test
enter param G(part,Fe,Ni:*:*:Fe,Ni),,GSRO; 6000 N test
enter param G(part,*:Fe,Ni:Fe,Ni:*),,GSRO; 6000 N test
enter param G(part,*:Fe,Ni:*:Fe,Ni),,GSRO; 6000 N test
enter param G(part,*:*:Fe,Ni:Fe,Ni),,GSRO; 6000 N test

amend biblio test VASP calculation by test;

@$ These are possible disordered parameters
enter param GD(part,Fe,Ni;0),,LD0+L0; 6000 N test
enter param GD(part,Fe,Ni;1),,LD1; 6000 N test
enter param GD(part,Fe,Ni;2),,LD2; 6000 N test

@$ enter param GD(part,Fe,Ni;0),,+L0; 6000 N test


list data ,,

@$ we have to create composition sets manually

@$ this is by default Fe3Ni_L12
amend phase part comp-set y , ,
<.2 >.5
>.5 <.2
>.5 <.2
>.5 <.2

@$ this is by default FeNi_L10
amend phase part comp-set y , ,
<.2 >.5
<.2 >.5
>.5 <.2
>.5 <.2

@$ this is by default FeNi3_L12
amend phase part comp-set y , ,
<.2 >.5
<.2 >.5
<.2 >.5
>.5 <.2


set c t=400 p=1e5 n=1 x(fe)=.3

c e

l r 2


@&
@$ Calculating the metastable regions are very sensitive to the step increment

set ax 1 x(fe) 0 1 0.02

step sep




@&



plot
x(ni)
GM(*)
title step 4 fig 1


@&
@$ the constitution of FeNi3

plot
x(ni)
y(part#4,*)
title step4 fig 2 L1_2


@&
plot


title step4 fig 3 L1_2
position top left
render

@&
@$ The constitution of the L1_0 phase (AlNi)
plot

y(part#3,*)
title step4 fig 4 L1_0
position top left
render

@&
@$ The calculation has sometimes failed for compositions
@$ where the L1_2 or L1_0 ordering is not stable
@&
@$ ========================================================
@$ Now something different
@$ 
@$ During a phase field simulation one may use mole fractions
@$ to calculate diffusion also for ordered phases. To find
@$ how the CONSTITUTION changes in an ordered phase when
@$ the MOLE FRACTION changes one must minimize the
@$ Gibbs energy for that phase.  
@$ There is a special command (and subroutine) to calculate 
@$ the constitution of a phase for a given set of mole fractions
@$ which could be useid when for a gridpoint with just one
@$ stable phase.  This command also gives the chemical potentials.
@$
@$ This is a way to avoid a full equilibrium calculation 
@$ but if there are two or more phases stable at the gridpoint
@$ one must do that in order to determine the phase amounts.
@$ However, an equilibrium calculation is sometimes needed to 
@$ find the most stable configuration ...
@&
@$ We use the current ordered FCC as an example, first
@$ calculate a full equilibrium at T=400 K and x(fe)=.27

l sh a

set c x(fe)=.27

l c

c e

l,,,,

@$
@$ At this composition and T we have an L1_2 ordered fcc phase
@&
@$ ============================================================
@$ Now calculate for a single phase with mole fraction Fe 0.26

calc phase 
part 
1 
y 
con
.26

@$
@$ Note the chemical potentials/RT above
@$ they are different the equilibrium listing higher up
@$ The value of G is also different but as it is divided by RT
@$ it is not so easy to compare
@&
@$ If we list the full equilibrium we get a warning

l,,,

@$
@$ because the constitution and composition listed is from the 
@$ single phase calculation whereas the conditions on the 
@$ composition and chemical potentials and the Gibbs energy
@$ has not changed and are inconsistent.
@&
@$ If we set the condition of Fe and calculate

set c x(fe)=.26

c e

l,,,,,

@$ then we get the same chemical potentials/RT
@&
@$ ============================================================
@$ Try a calculate phase with a different composition, x(fe)=.49
@$ 
calc phase 
part 
1 
y 
con
.49

l,,,

@$
@$ The phase is still L1_2 ordered as the calculation
@$ used the previous constitution as start values.  
@$ Note the Gibbs energy/RT= -3.0747
@&
@$ ============================================================
@$ To have the fcc L1_0 ordered we have to give a start constitution
@$ NOTE it is important to start from extreme ordering

calc phase 
part 
1 
n
.01
.01
.99
.99
con
.49

l,,,

@$
@$ Now the fcc phase is L1_0 ordered 
@$ and the Gibbs energy/RT is more negative, -3.1546
@$
@$ NOTE: If you start from a less ordered state like 0.1/0.9 
@$ you may find a less stable L1_2 phase.
@&
@$ ============================================================
@$ Calculate also setting start constitution as disordered

calc phase 
part 
1 
n
.5
.5
.5
.5
con
.49

l,,,


@$
@$ The Gibbs energy/RT is -3.1150
@$ so the L1_0 ordered state is more stable than the
@$ disordered which is more stable than the L1_2
@&
@$ ============================================================
@$ A full equilibrium calculation is sometimes necessary to
@$ determine the most stable configuration of a single phase
@$ using the grid minimizer (or manually set start constitutions)
@$ We can check what the grid minimizer gives for x(Fe)=0.49

set c x(fe)=.49

c e

l,,,,


@$ But again, 
@$ sometimes one may want to use a metastable state...
@&
set inter

