new Y


set etco Y

@$ ===================================================================
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$
@$ step5.OCM
@$ Calculate y and Cp as function of T for the ordered FCC FeNi3
@$ ===================================================================
@&

set echo

@$ Enter the elements and their reference states
enter element Fe Iron BCC 55.847 0 0 

enter element Ni Nickel FCC 58.69 0 0

@$ These functions describe the end-member energies at Fe3Ni, Fe2Ni2 and FeNi3
@$ respectivly.  The VASP energies relative to pure Fe amd Ni as fcc are:
@$ Fe3Ni1 -0.071689 eV for 1 atom??
@$ Fe2Ni2 -0.138536 eV for 1 atom??
@$ Fe1Ni3 -0.125748 eV for 1 atom??
@$ To modify to J/mol atoms multiply with 96500
@$ bond energy multiplied with 3, 4 and 3 respectively.

enter tp-sym evtoj constant 96500

enter tp-sym GA3B1 fun 1 -0.071689*evtoj;,,,,,
enter tp-sym GA2B2 fun 1 -0.138536*evtoj;,,,,,
enter tp-sym GA1B3 fun 1 -0.125748*evtoj;,,,,,
 
@$ We can use a disordered regular solution parameter
enter tp-sym L0 fun 1 12000; ,,,,,

@$ this is an approximate SRO contribution to the LRO phase.  It is
@$ set to about a quater of the L1_0 ordering energy, 
@$ equal to the Fe-Ni bond energy
enter tp-sym GSRO fun 1 -0.034*evtoj;,,,,,

@$ Using the partitioned model the contribition from the ordered parameters
@$ will cancel when the phase is disordered.  If we want them to contribute
@$ we must add them to the disordered part
enter tp-sym LD0 fun 1 GA3B1+1.5*GA2B2+GA1B3+1.5*GSRO;,,,,,,
enter tp-sym LD1 fun 1 2*GA3B1-2*GA1B3;,,,,,,
enter tp-sym LD2 fun 1 GA3B1-1.5*GA2B2+GA1B3-1.5*GSRO;,,,,,,

@$ ==================================================
@$ This is an fcc phase with lro but no explicit sro
@$ described with the sublattice model
enter phase PARTITIONED_FCC 
CEF 
4 .25 Fe NI; .25 Fe NI; .25 Fe NI; .25 Fe NI;

@&
@$ we must add disordered set before entering parameters
amend phase part dis 4 yes

enter param G(part,Fe:Fe:Fe:Ni),,GA3B1; 6000 N test
enter param G(part,Fe:Fe:Ni:Fe),,GA3B1; 6000 N test
enter param G(part,Fe:Ni:Fe:Fe),,GA3B1; 6000 N test
enter param G(part,Ni:Fe:Fe:Fe),,GA3B1; 6000 N test
enter param G(part,Fe:Ni:Ni:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Fe:Ni:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Ni:Fe:Ni),,GA1B3; 6000 N test
enter param G(part,Ni:Ni:Ni:Fe),,GA1B3; 6000 N test
enter param G(part,Fe:Fe:Ni:Ni),,GA2B2; 6000 N test
enter param G(part,Fe:Ni:Fe:Ni),,GA2B2; 6000 N test
enter param G(part,Ni:Fe:Fe:Ni),,GA2B2; 6000 N test
enter param G(part,Fe:Ni:Ni:Fe),,GA2B2; 6000 N test
enter param G(part,Ni:Fe:Ni:Fe),,GA2B2; 6000 N test
enter param G(part,Ni:Ni:Fe:Fe),,GA2B2; 6000 N test

enter param G(part,Fe,Ni:Fe,Ni:*:*),,GSRO; 6000 N test
enter param G(part,Fe,Ni:*:Fe,Ni:*),,GSRO; 6000 N test
enter param G(part,Fe,Ni:*:*:Fe,Ni),,GSRO; 6000 N test
enter param G(part,*:Fe,Ni:Fe,Ni:*),,GSRO; 6000 N test
enter param G(part,*:Fe,Ni:*:Fe,Ni),,GSRO; 6000 N test
enter param G(part,*:*:Fe,Ni:Fe,Ni),,GSRO; 6000 N test

amend biblio test VASP calculation by test;

@$ These are possible disordered parameters
enter param GD(part,Fe,Ni;0),,LD0+L0; 6000 N test
enter param GD(part,Fe,Ni;1),,LD1; 6000 N test
enter param GD(part,Fe,Ni;2),,LD2; 6000 N test


list data ,,


@&
@$ set a slightly off-ideal composition as the griminimiser prefers that ...
set c t=300 p=1e5 n=1 x(ni)=.751


@$ To avoid confusion I do no calculate with 2 composition sets
@$ I do not want to use the gridminimizer.  If I just use "c n"

c n

l res 2


@$ I get the disordered phase.  Its G=-8089 J/mol
@$ To set the constitution to be ordered I use the calculate phase comamnd

calc phase
part
1
N
.1
.1
.1
.9

@$ There should be low fraction Fe in 3 sublattices and high in the 4th
@$ After this we can use the "c n" again which will start from current y

c n

l,,,,,

@$ Now the phase is ordered with a lower G=-9858 J, 1 kJ more negative!

@&

@$ Set a T axis to calculate how the ordering varies wih T

set ax 1 T 10 800 5

step 




@&

plot
T
y(part,*)
title step 5 fig 1
render


@&

@$ Plot also the heat capacity

ent sym cp=h.t;

c sym cp

@&

plot
T
cp
title step 5 fig 2a


@&

@$ We may need scaling ...

plot
T
cp
scale
y
N
0
10
title step 5 fig 2b
render


@&


@$==========================================================================
@$ end of step5 macro
@$==========================================================================

set inter



